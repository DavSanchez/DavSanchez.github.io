---
title: "Entornos de desarrollo reproducibles con Nix"
date: 2024-06-26
---
<!-- LTeX: language=es -->

Tras la introducci√≥n a Nix que vimos en el [art√≠culo anterior](./2024-06-16-conociendo-nix.md), continuamos con uno de los casos de uso m√°s potentes de esta herramienta.

> [!warning]
> De forma similar al [art√≠culo anterior](./2024-06-16-conociendo-nix.md), a partir de aqu√≠ asumiremos que has instalado Nix y activado dos configuraciones opcionales que facilitan mucho la experiencia de usuario con Nix. Si usaste el instalador oficial, puedes activarlas individualmente para cada comando del terminal usando `nix --extra-experimental-features "nix-command flakes"`, o activarlas globalmente a√±adiendo lo siguiente a `~/.config/nix/nix.conf` o `/etc/nix/nix.conf`:
>
> > ```conf
> > experimental-features = nix-command flakes
> > ```

> [!info]
> A no ser que se indique lo contrario, los ejemplos a continuaci√≥n est√°n hechos en un sistema **macOS** utilizando **zsh** como shell por defecto.

Imagina que tienes un proyecto de software en el que est√°s trabajando, quiz√° en las etapas iniciales. No tienes inter√©s en empaquetar el producto en Nixpkgs ni nada parecido, porque quiz√° a√∫n no tienes nada que lanzar a√∫n, pero s√≠ que te gustar√≠a:

- Asegurarte de que tienes todas las herramientas que tienes para desarrollar este proyecto, como por ejemplo:
  - La versi√≥n *nightly* de Rust con varios objetivos de compilaci√≥n diferentes al por defecto, como WASM u otros.
  - Una versi√≥n de Go espec√≠fica, diferente a una que puedas tener instalada globalmente v√≠a otras herramientas como Homebrew.
  - La CLI de AWS/Ansible/Terraform.
  - Alguna utilidad de Python que normalmente instalar√≠as con `pip`, como `jmespath`.
  - Variables de entorno espec√≠ficas fijadas a un valor particular (`AWS_PROFILE` o alguna configuraci√≥n de git).
- Asegurarte de que tus *pipelines* de CI/CD tienen estas mismas herramientas, en las mismas versiones, para que tu equipo pueda ejecutar localmente las mismas acciones que la *pipeline* CI/CD y obtener el mismo resultado.
- Forzar un estilo particular de mensajes en los *commits*, o ejecutar las mismas herramientas (formateadores, *linters* o una herramienta propia) con la misma configuraci√≥n cada vez que un miembro del equipo crea una revisi√≥n o *commit*.
- Prevenir que las herramientas que has instalado para tu proyecto entren en conflicto con otros proyectos de tu equipo, sin por ello tener que utilizar contenedores:
  - El Proyecto A usa alg√∫na funcionalidad solo disponible en Go 1.20 y posterior.
  - El Proyecto B no est√° listo para actualizar m√°s all√° de Go 1.19 por ciertos problemas con CGO y `libresolv`.
- Hacer que cada nueva incorporaci√≥n al equipo est√© listo para usar todas estas herramientas y convenciones f√°cilmente.

¬øC√≥mo podr√≠as lograr todo esto sin recurrir a contenedores, Docker y dem√°s?

Averig√º√©moslo.

Recuerda que una de las posibles [*salidas*](./2024-06-16-conociendo-nix.md) que pueden definirse en un *Nix flake* son **entornos de desarrollo**. Esto significa que, usando Nix, puedes ir al repositorio de tu proyecto, "convertirlo" en un *flake* (tan solo a√±adiendo un fichero `flake.nix` en su ra√≠z), y con solo algunas instrucciones en el lenguaje Nix puedes configurar todo lo que mencionamos en la lista. A√±ade el fichero a tu control de versiones y todos los miembros de tu equipo, las *pipelines* CI/CD, todo el mundo podr√° comenzar a usarlo.

No m√°s "pues en mi m√°quina funciona" o instalar la misma herramienta con `apt-get` en Ubuntu, `Dockerfile` con `FROM ubuntu:latest` en macOS, una GitHub Action en tu CI/CD y golpearte la cabeza contra la pared cuando alguno de los tres entornos falla.

## Nuestro primer *flake*

> [!tip]
> Esta seguramente sea tu primera exposici√≥n al lenguaje de programaci√≥n Nix. No entraremos en demasiado detalle por ahora, confiando en que no ser√° dif√≠cil intuir lo que ocurre en los comienzos, pero si necesitas recursos para profundizar, echa un vistazo a [esta web interactiva](https://zaynetro.com/explainix) o al [tutorial oficial](https://nix.dev/tutorials/nix-language.html). ¬°O no dudes en preguntarme!

### Estructura b√°sica

Un fichero `flake.nix` contiene lo que en lenguaje Nix se denomina un **conjunto de atributos** (*attribute set* o *attrset* para abreviar), algo muy similar a un objeto JSON:

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {}; # Omitimos los contenidos de momento

  outputs = {}; # Esto que lees es un comentario
}
```

Recordemos que Nix hace que nuestras *builds* sean declarativas y **reproducibles**. Cualquier salida que definamos vendr√° determinada por las entradas que definamos, y por nada m√°s. Veamos, entonces, c√≥mo se definen estos *inputs* (entradas) y *outputs* (salidas).

#### Entradas

Para declarar paquetes como dependencias, estos paquetes tienen que venir de alguna parte. ¬øDe d√≥nde vienen, pues? ¬øQui√©n los define?

La respuesta la tenemos en el art√≠culo anterior, en el que mencion√© que Nixpkgs es un repositorio de m√°s de 100000 paquetes instalables. Declaremos entonces Nixpkgs, que tambi√©n es un *flake*, como entrada:

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {
    # Puedes darle un nombre cualquiera a tu input, lo que determina qu√© es es la URL.
    nixpkgs = {
      # Consulta el art√≠culo anterior para recordar qu√© significaba esta referencia.
      url = "github:NixOS/nixpkgs/nixos-23.11";
    };
  };

  outputs = {};
}
```

Esta sintaxis es √∫til si queremos personalizar cada entrada, pero no cubriremos esto en este art√≠culo. Si solo queremos a√±adir entradas de una forma b√°sica, solo necesitando su URL, podemos hacer esto en su lugar:

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs.nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.11";
  inputs.git-hooks.url = "github:cachix/git-hooks.nix";

  outputs = {};
}
```

O agrupar las entradas de esta otra forma:

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.11";
    git-hooks.url = "github:cachix/git-hooks.nix";
  };

  outputs = {};
}
```

#### Salidas

Aqu√≠ es donde se pone interesante. Las salidas definen lo que ofrece nuestro *flake*, y deber√≠an utilizar de alguna forma las entradas que hemos definido anteriormente. Como ya hemos comentado, Nix recibe mucha influencia de la programaci√≥n funcional, por tanto, **la salida est√° definida como una funci√≥n que recibe las entradas como par√°metro, y devuelve un nuevo conjunto de atributos**. Las funciones en Nix se definen con la forma `myFunction = arg1: arg2: ... argN: returned_value` (s√≠, esto es [currying](https://en.wikipedia.org/wiki/Currying)). Por tanto, para nuestra salida:

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.11";
    git-hooks.url = "github:cachix/git-hooks.nix";
  };

  outputs = {
    nixpkgs,
    git-hooks
  }: {}; # Esto √∫ltimo es el conjunto de atributos que devuelve la funci√≥n. Vac√≠o de momento.
}
```

Sup√≥n que hemos definido muchas entradas, pero solo un peque√±o subconjunto de ellas es importante y queremos que exista una referencia f√°cil a ellas en todo momento, mientras que las dem√°s solo se referenciar√°n ocasionalmente. Podemos des-estructurar el conjunto de atributos de la siguiente forma (que reconocer√°n los programadores de Haskell o Rust):

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.11";
    git-hooks.url = "github:cachix/git-hooks.nix";
  };

  outputs = inputs @ { # Vinculamos todas las entradas a "inputs".
    nixpkgs,           # Queremos referencia f√°cil a nixpkgs
    ...                # Omitimos lo dem√°s
  }: {
                       # Ahora podemos usar las referencias aqu√≠.
                       # Ya sea refiri√©ndose a "nixpkgs" directamente o,
                       # si queremos acceder a git-hooks, usando
                       # "inputs.git-hooks".
  };
}
```

##### A√±adiendo dependencias

Ya tenemos la forma b√°sica y queremos tener algo disponible r√°pidamente. Definamos nuestro entorno de desarrollo, una *shell* personalizada con el atributo [**devShell**](https://nixos.org/manual/nixpkgs/stable/#sec-pkgs-mkShell):

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.11";
    git-hooks.url = "github:cachix/git-hooks.nix";
  };

  outputs = {
    nixpkgs,
    ...
  }: let
    macM1Packages = nixpkgs.legacyPackages.aarch64-darwin;
    linuxAMD64Packages = nixpkgs.legacyPackages.x86_64-linux;
  in {
    devShells.aarch64-darwin.default = macM1Packages.mkShell {
      packages = [
        # Al momento de escribir esto, la versi√≥n de
        # Go_1_19 en nixpkgs/nixos-23.11 es 1.19.13
        macM1Packages.go_1_19
      ];
    };

    devShells.x86_64-linux.default = linuxAMD64Packages.mkShell {
      # Nada por ahora
    };
  };
}
```

###### ¬øEs obligatorio definir *devShells* y usar referencias a los conjuntos de paquetes para cada sistema por separado?

**La respuesta r√°pida es *¬°No!***.

La respuesta larga es que Nix sirve para construir paquetes, una herramienta o sistema de *builds*, y que por tanto es necesario poder ser consciente del sistema o *target* para el que estamos construyendo un paquete, *shell* o salida en general, pues en general un software no puede ser compilado sin m√°s para todos los posibles sistemas que existen.

Dicho esto, para el caso que nos ocupa en este tutorial, combinaciones de sistema operativo y arquitectura bastante extendidas y de prop√≥sito general, s√≠ que podemos asumir que cierto software pueda compilar para ellas sin demasiado problema. Otros desarrolladores han hecho el trabajo duro por nosotros y ofrecen algunas funciones y sistemas para minimizar la repetici√≥n, a cambio de un poquito m√°s de c√≥digo Nix. Una de estas utilidades es [`flake-utils`](https://github.com/numtide/flake-utils). Para utilizarla imagino que ya intuir√°s lo que necesitamos: Definirla como entrada.

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.11";
    git-hooks.url = "github:cachix/git-hooks.nix";
    flake-utils.url = "github:numtide/flake-utils"; # Nuevo input!
  };

  outputs = {
    nixpkgs,
    flake-utils,
    ...
  }: flake-utils.lib.eachDefaultSystem (system: let
    pkgs = nixpkgs.legacyPackages.${system};
  in {
    devShells.default = pkgs.mkShell {
      packages = with pkgs; [
        go_1_19
      ];
    };
  });
}
```

Este `eachDefaultSystem` que hemos utilizado es una funci√≥n que recibe otra funci√≥n como par√°metro (ya habl√© de la programaci√≥n funcional), y esta funci√≥n a su vez recibir√≠a un par√°metro que llamamos `system`, que utilizamos en el cuerpo de la funci√≥n para definir el conjunto de paquetes que definir√° nuestro *devShell*. ¬øQu√© argumento de `system` usa esa funci√≥n? De eso se encarga `eachDefaultSystem`, que pasa una lista con los siguientes valores:

- `x86_64-linux`
- `aarch64-linux`
- `x86_64-darwin`
- `aarch64-darwin`

> [!tip]
> Para comenzar de forma simple he sugerido utilizar `flake-utils`, pero para algo m√°s avanzado y modular, que uso actualmente en mis *flakes*, recomiendo [`flake-parts`](https://flake.parts).

Vamos a ver cu√°les son las salidas de nuestro *flake* ejecutando `nix flake show` en el directorio ra√≠z de tu *flake* (o, ya sabes, `nix flake show "path:/ruta/a/tu/flake"`). Nix tardar√° un tiempo en evaluar el c√≥digo Nix de tu *flake*, descargar las entradas y evaluar las salidas, pero finalmente se mostrar√° algo parecido a esto:

```console
$ nix flake show
warning: creating lock file '/Users/david/random/flake.lock'
path:/Users/david/random?lastModified=1695943648&narHash=sha256-CbZ16gnffi0%2B9ig270ifpbHYOKS4CreKnH7GlPi
‚îî‚îÄ‚îÄ‚îÄdevShells
    ‚îú‚îÄ‚îÄ‚îÄaarch64-darwin
    ‚îÇ   ‚îî‚îÄ‚îÄ‚îÄdefault: development environment 'nix-shell'
    ‚îú‚îÄ‚îÄ‚îÄaarch64-linux
    ‚îÇ   ‚îî‚îÄ‚îÄ‚îÄdefault omitted (use '--all-systems' to show)
    ‚îú‚îÄ‚îÄ‚îÄx86_64-darwin
    ‚îÇ   ‚îî‚îÄ‚îÄ‚îÄdefault omitted (use '--all-systems' to show)
    ‚îî‚îÄ‚îÄ‚îÄx86_64-linux
        ‚îî‚îÄ‚îÄ‚îÄdefault omitted (use '--all-systems' to show)
```

El fichero `flake.lock` es un *lockfile*, que tiene un prop√≥sito similar a los probablemente m√°s conocidos `package-lock.json` para proyectos del ecosistema JavaScript, `Gemfile.lock` para Ruby, `Cargo.lock` para Rust, etc. Contendr√° los *hashes* de todas las entradas de nuestro *flake*, asegurando la reproducibilidad e integridad de nuestras entradas.

#### Entrando en la *shell*

Entonces, ¬øqu√© falta? ¬øC√≥mo accedemos a la versi√≥n de Go que hemos declarado en este entorno de desarrollo? Podemos hacerlo con `nix develop`:

```console
$ which go
/opt/homebrew/opt/go # Ejemplo de una instalaci√≥n de Go existente

$ go version
go version go1.20.8 darwin/arm64

$ nix develop
# Se tomar√° su tiempo, pero despu√©s...

$ which go
/nix/store/3yndvq32rxh6h9bqjd6n20npk2ix0ah2-go-1.19.13/bin/go

$ go version
go version go1.19.13 darwin/arm64
```

¬°Listo! Como si fuera un `virtualenv` de Python, pero para cualquier lenguaje y m√°s, usar `nix develop` nos introduce en una nueva *shell* con los paquetes que definimos disponibles en el `$PATH`. ¬°De forma nativa! ¬°Sin contenedores, vol√∫menes, etc!

Ahora escribe algo de c√≥digo, comp√≠lalo[^go-compile], y ya hablaremos de c√≥mo empaquetar tu programa de Go con Nix en un futuro art√≠culo.

Para salir de esta *shell* y volver al estado anterior a utilizar `nix develop`, simplemente ejecuta `exit` o usa `Ctrl + D`.

Por supuesto, podr√°s intuir que esto no es lo √∫nico que se puede hacer en cuanto a definir *shells* o empaquetar software. Puedes hacer chequeos automatizados, usar *shell hooks* para ejecutar acciones al entrar en la *shell*, configurar *pre-commit hooks* para personalizar tu trabajo con `git`, referenciar otros ficheros dentro del *flake* (como una configuraci√≥n en formato YAML) con `import`, definir *bundlers* y empaquetar el software para sistemas que no usen Nix... ¬°Y mucho m√°s!

> [!tip]
> Tampoco es completamente necesario entrar a picar c√≥digo Nix y definir todo esto a mano. Aunque lo recomiendo para que saber m√°s o menos lo que ocurre, hay muchas iniciativas para abstraer muchos de los mecanismos internos y comenzar a trabajar m√°s r√°pidamente.
>
> El que puedo recomendar es [`devenv.sh`](https://devenv.sh), cuyo autor es un contribuidor principal del ecosistema (tambi√©n autor de [`cachix`](https://www.cachix.org), `git-hooks.nix` y algunas GitHub Actions para instalar Nix en los *runners*). `devenv.sh` tambi√©n se configura con el lenguaje Nix, pero de forma bastante sencilla.
>
> Tambi√©n hay alternativas que utilizan YAML o JSON, como [Flox](https://flox.dev) o [DevBox](https://www.jetify.com/devbox), respectivamente. Estos √∫ltimos apenas los conozco, y aunque pueden ofrecer algunas comodidades, en mi opini√≥n parecen abstraer demasiado lo que ocurre y podr√≠an no cubrir todos los casos de uso particulares.
>
> Como ves, el ecosistema es [muy amplio](https://nix-community.github.io/awesome-nix/).

Probablemente tambi√©n tengas muchas preguntas. Trato de responder a algunas de ellas en el apartado siguiente, enlazando a recursos relevantes, aunque puedo desarrollar estos temas en futuros art√≠culos o si me preguntas directamente.

## Algunas preguntas y respuestas r√°pidas

### ¬øC√≥mo s√© si un paquete que necesito est√° disponible en Nixpkgs?

Puedes buscar [aqu√≠](https://search.nixos.org/packages). Si utilizas DuckDuckGo, puedes acceder a esta b√∫squeda r√°pidamente con `!nixpkgs`. Por ejemplo, usa `!nixpkgs python` para ver qu√© paquetes relacionados con Python hay en Nixpkgs.

### Trabajar con *devShells* descarga cosas en mi sistema. ¬øC√≥mo limpio lo que ya no necesito?

Aunque puede dar la impresi√≥n de que no *instalas* nada de forma permanente en tu m√°quina cuando configuras todos estos entornos de desarrollo y descargas paquetes con Nix, obviamente todos estos recursos est√°n en tu sistema y ocupan espacio.

Nix usa un recolector de basura para eliminar contenido de la Nix Store que no utilizas. Este recolector puede configurarse para ejecutar regularmente, o puedes llamarlo directamente con `nix store gc` (no necesitas estar dentro de un *flake* o un *devShell* para ello).

Como intuir√°s, hay mucho m√°s detr√°s de esta operaci√≥n. ¬øC√≥mo puede Nix identificar lo que est√° en uso y lo que no? ¬øEst√° esto relacionado con los enlaces simb√≥licos que se crearon cuando ejecutamos `nix build` en [el tutorial anterior](./2024-06-16-conociendo-nix.md)?

Puedes leer m√°s [aqu√≠](https://nixos.org/guides/nix-pills/11-garbage-collector.html) y [aqu√≠](https://nixos.wiki/wiki/Storage_optimization).

### ¬øTengo que escribir siempre los *flakes* desde cero?

Existe un mecanismo para descargar plantillas, que tambi√©n pueden exponerse como salidas de un *flake*. Por ejemplo, yo mantengo un *flake* con [algunas de ellas](https://github.com/DavSanchez/nix-dotfiles/tree/master/templates) (aunque no las he actualizado para que usen `flake-parts` a√∫n).

- Para proyectos de Rust, con algunas utilidades para configurar las *toolchains* (utilizando [fenix]) y *git hooks*. Puedes descargarla con `nix flake init -t "github:DavSanchez/nix-dotfiles#rust_fenix_precommit"`.
- Para proyectos de Go, con *git hooks*, usa `nix flake init -t "github:DavSanchez/nix-dotfiles#go_precommit"`
- Para proyectos con Haskell y *git hooks*, usa `nix flake init -t "github:DavSanchez/nix-dotfiles#haskell_precommit"`.

### ¬øC√≥mo configuro los *git hooks*?

Si le√≠ste el apartado anterior, ver√°s que menciono repetidamente los *git hooks*. En mi opini√≥n son una herramienta muy √∫til para asegurarte de que tu c√≥digo cumple ciertos est√°ndares antes de realizar un *commit* (¬øpasa los tests unitarios? ¬øEst√° formateado?) o para usar alg√∫n formato en tus mensajes de *commit*, como [*conventional commits*](https://www.conventionalcommits.org/en/v1.0.0/).

Mis plantillas definen como entrada [`cachix/git-hooks.nix`](https://github.com/cachix/git-hooks.nix) para definir estos *hooks* con Nix y cargarlos directamente en tu *devShell* con *shellHook*. Echa un vistazo al repositorio de `git-hooks.nix` para ver qu√© *hooks* est√°n disponibles y c√≥mo crear los tuyos propios.

### ¬øC√≥mo personalizo mis *devShells*?

Esto depende de tus preferencias personales. `nix develop` utiliza Bash por defecto. Yo utilizo [`zsh-nix-shell`](https://github.com/chisui/zsh-nix-shell), un plugin que permite usar ZSH como *devShell*, y tambi√©n [`starship`](https://starship.rs/) para personalizar el *prompt*.

### ¬øTengo que ejecutar *nix develop* cada vez que quiero entrar en la *devShell*?

Puedes automatizar esto gracias a [`direnv`](https://direnv.net). Si echaste un vistazo a mis plantillas ver√°s que incluyen un fichero `.envrc`. Este fichero ayuda a conseguir este comportamiento. Si me muevo dentro de un directorio que es un *flake* con `cd`, la *devShell* se activa autom√°ticamente. Si hago `cd` y salgo de la *shell*, se desactiva.

Hay un art√≠culo en el [blog de Determinate Systems](https://determinate.systems/posts/nix-direnv) con m√°s detalles.

### ¬øQu√© m√°s se puede hacer?

¬øPodr√≠a usar Nix en mis l√≠neas de CI/CD con [GitHub Actions](https://nix.dev/tutorials/nixos/continuous-integration-github-actions)? ¬øPuedo configurar todo mi [sistema](https://github.com/DavSanchez/nix-dotfiles/) o mi [usuario](https://github.com/nix-community/home-manager) de forma declarativa con Nix? ¬øAunque est√© en [macOS](https://github.com/LnL7/nix-darwin)? ¬øQu√© hay de [NixOS](https://search.nixos.org/options?query=options)?

Probablemente exploremos todas estas cosas en futuros art√≠culos. De momento ah√≠ tienes algunos enlaces a documentaci√≥n y plantillas para ir abriendo boca.

¬°Nos vemos en el siguiente!

[^go-compile]: Para los lenguajes compilados para Linux y que usen enlazado din√°mico (como Go al habilitar CGO) hay ciertos detalles que no hemos cubierto en este art√≠culo y que te puedes encontrar si avanzas m√°s por tu cuenta. Si las rutas de todas las dependencias est√°n en la Nix Store, d√≥nde espera un binario con *dynamic linking*, generado con Nix, encontrar al *dynamic loader*? ¬øSigue siendo en rutas como `/lib64/ld-linux-x86-64.so.2`? üôÉ
